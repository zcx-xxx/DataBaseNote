# 第八章 数据库编程

## 8.1 编程基础

**变量的声明，类型转换**

~~~sql
--局部变量的声明，可以同时声明多个
declare @mytest char(20), @cnt int

--赋值，set一行只可以为一个局部变量赋值
--输出，print一次只可以打印一个局部变量的值（不是以表格的形式打印）
set @mytest='hello world'
print @mytest

--赋值，select一行可以为多个局部变量赋值
--输出，select一行可以输出多个局部变量的值（以表格的形式输出）
select @mytest = 'zcx', @cnt = 1
select @mytest, @cnt

--一个显示日期的小例子
declare @var1 date, @var2 char(10)      --声明两个局部变量
set @var1 = GETDATE()                   --获得当前的时间
--强制类型转换，arg1:转换成的变量的数据类型,arg2:要进行转换的变量，arg3:状态码
set @var2 = CONVERT(char(10), @var1, 102) 
print @var1
print @var2
/*
2019-05-15
2019.05.15
*/
~~~

>  注:
>
> 1. 局部变量在声明之后不可以直接使用，需要进行赋值
> 2. 变量之间的赋值一般使用set

**分支，循环，判断，等待...**

~~~sql
--简单分支
/*
功能：如果存在学号为201701181的学生，则打印该学生的信息，否则输出NO
*/
if exists(select * from student where Sno = '201701181')
	begin
        select *
        from student
        where Sno = '201701181'
	end
else 
	begin
        print 'NO'
	end

--简单case语句
/*
如果ssex为男，以M显示 否则以F显示
*/
select sno, ssex = 
			case ssex
				when '女' then 'F'
				when '男' then 'M'
			end
from student;

--搜索式case语句
select sno, grade = 
					case
						when grade >= 0 and grade < 60 then '不及格'
						else '及格'
					end
from student;

--while循环
/*
计算1~10的和
*/
declare @var int, @sum int
set @var = 0
set @sum = 0

while @var < 10
begin
	set @var = @var + 1
	set @sum = @sum + @var
end
print @sum

--延迟语句
/*
延迟十秒执行
*/
waitfor delay '00:00:10'
select * from student;

/*
等到18:00:00执行
*/
waitfor time '18:00:00'
select * from student;
~~~

> 注：
>
> 1. 在case语句中可以使用else子句
> 2. if语句后边加条件表达式，不需要括号，语句块使用begin . . . end声明。

## 8.2 游标的使用

> 游标的使用步骤：
>
> 1. 声明一个游标
> 2. 打开一个游标
> 3. 取游标中的数据
> 4. 关闭一个游标
> 5. 释放一个游标

~~~sql
/*
第一个小例子：使用游标实现查询未选课的学生的人数
*/
--声明游标
declare myCursor cursor
for
	select sno
	from student
for read only

--打开游标
open myCursor

--读取游标中的数据
declare @num char(9), @cnt int

set @cnt = 0
--游标向下移动一个单位，并将所指向的位置的值取出放入指定的变量中
fetch next from myCursor into @num

--单等号判等,全局变量判断fetch是否执行成功，成功返回0，越界返回-1，数据不存在返回-2
while @@FETCH_STATUS = 0
begin
	if not exists(select * from SC where Sno=@num)
		set @cnt = @cnt + 1
	fetch next from myCursor into @num
end

print @cnt

--关闭游标
close myCursor

--释放游标
deallocate myCursor
~~~

~~~sql
/*
第二个小例子：统计各个等级的学生人数
*/
declare mycur cursor
for 
	select Grade
	from SC
for read only

open mycur

declare @grade int, @cnt1 int, @cnt2 int, @cnt3 int, @cnt4 int, @cnt5 int
select @cnt1 = 0, @cnt2 = 0, @cnt3 = 0, @cnt4 = 0, @cnt5 = 0
fetch next from mycur into @grade

while @@FETCH_STATUS = 0
begin
	if @grade >= 90
		set @cnt1 = @cnt1 + 1
	else if @grade >= 80
		set @cnt2 = @cnt2 + 1	
	else if @grade >= 70
		set @cnt3 = @cnt3 + 1
	else if @grade >= 60
		set @cnt4 = @cnt4 + 1	
	else
		set @cnt5 = @cnt5 + 1	
	fetch next from mycur into @grade	
end

select @cnt1, @cnt2, @cnt3, @cnt4, @cnt5

close mycur

deallocate mycur
~~~

> 注意：
>
> * 声明游标的过程与其他的过程不同，首先写游标的名字，在写关键字（cursor），和声明局部变量的形式相似
> * 游标如果第一次执行失败，那么再执行第二次的时候需要将之前的删掉，重新声明

## 8.3 存储过程

~~~sql
/*查询不及格课程数超过三门的学生的信息*/
--创建不带参数的存储过程
/*
 * procedure 也可以写成proc
 */
create procedure myproc
as
begin
	select *
	from student
	where Sno in(
		select Sno
		from SC
		where grade < 60
		group by Sno
		having COUNT(cno) > 3
	)
end

--执行
exec myproc
~~~

~~~sql
/*实现插入功能*/
--带输入参数的存储过程
create proc myproc2
@xuehao char(9),
@name char(8),
@sage smallint,
@ssex char(2),
@sdept char(8)
as
begin 
	insert into student 
	values(@xuehao, @name, @sage, @ssex, @sdept);
end

exec myproc2 '201911111', 'haha', 30, '男', 'cs'
~~~

~~~sql
--带默认值的输入参数
create proc myproc3
@xuehao char(9),
@name char(8),
@sage smallint,
@ssex char(2) = '女',
@sdept char(8)
as
begin
	insert into student
	values(@xuehao, @name, @sage, @ssex, @sdept)
end

exec myproc3 '201922222', 'kaka', 20, default, 'MA'
~~~

~~~sql
/*带输出参数的存储过程*/
--注意out关键字
create proc myproc4
@xuehao char(9),
@ping int out
as
begin
	select @ping = AVG(grade)
	from sc
	where sno = @xuehao
end

declare @avg int
exec myproc4 '201922222', @avg out
print @avg
~~~

> 注意：
>
> 1. 两个参数，一个参数是输入参数，一个是输出参数，输出参数需要 + out关键字
> 2. 在执行存储过程的时候，传入的实际输出参数后边也需要加 out 关键字
> 3. 执行存储过程使用exec关键字

~~~sql
/*删除存储过程*/
drop proc myproc;
~~~

## 8.4 自定义函数

**标量函数**

~~~sql
/*传入一个时间，返回日期部分*/
create function myfunc(@riqi datetime)
returns varchar(12)
as
begin 
	return convert(varchar(12), @riqi, 102)
end

--调用函数需要加模式名
select dbo.myfunc(GETDATE())
~~~

> 注意：
>
> * 调用函数需要dbo，执行存储过程的时候不用
> * 自定义函数必须以return结尾
> * 在定义自定函数的时候，指明返回值类型是使用的是returns，而不是return

~~~sql
/*使用自定义函数判断一个人的出生年代*/
create function whichgeneration(@birthday datetime)
returns varchar(20)
as
begin
	if YEAR(@birthday) between 1980 and 1990
		return '80s'
	if YEAR(@birthday) between 1990 and 2000
		return '90s'
	return 'too old'
end

print dbo.whichgeneration('1999-02-25')
~~~

> 注意：
>
> * 注意一下时间类函数的使用
> * 标量函数必须有显示的返回值

**内嵌表值函数**

~~~sql
/*查询学生表学生的学号和姓名*/
--返回结果是一个表
--调用的时候必须加上dbo
--不可以使用begin，end
create function func()
returns table
as
return(
	select sno, sname
	from student
)

select *
from dbo.func()
~~~

> 注意：
>
> * 内嵌表值函数的返回结果必须是table，即一个表
> * 绝对不可以加begin，end，否则报错
> * 遍历结果的时候需同遍历表的时候一样

## 8.5 触发器

**使用触发器实现参照完整性**

~~~sql
create trigger mytriger on sc
for insert
as
begin
	--将插入表中的元素赋值给@haha
	declare @haha char(9)
	select @haha = inserted.sno from inserted
	if not exists(select * from student where student.sno=@haha)
		delete from sc where sno = @haha
end

insert into sc values('222222222', '22', 5)
select *
from sc
~~~

> 注意：
>
> * 解释：上述语法表示在sc表的insert操作上定义触发器，当对sc表进行插入删除的时候触发触发器
> * 执行过程：在对sc表进行插入的过程中，首先将该条记录插入sc表中，然后同时将该条记录插入一个临时表inserted中，然后判断该条记录是否满足插入的条件，如果满足，不做任何处理，如果不满足，将刚才插入的记录进行删除。
> * insert前边不是必须使用for关键字，可以使用after或者instead of，其中for和after的效果一样，instead of触发器在用户执行insert，delete，update操作的时候不是直接进行，而是使用instead of触发器中指定的操作去代替用户直接的操作。

**触发器在执行过程中自动生成的两张表**

| 激活触发器的动作 | Inserted表       | Deleted表          |
| ---------------- | ---------------- | ------------------ |
| Insert           | 存放要插入的记录 |                    |
| Update           | 存放要更新的记录 | 存放更新前的旧记录 |
| Delete           |                  | 存放要除的旧记录   |

> 触发器触发时
>
> * 系统自动在内存中创建deleted表或inserted表
>
> * 只读，不允许修改；触发器执行完成后，自动删除
>
> Inserted 表 
>
> * 临时保存了插入或更新后的记录行 
>
> * 可以从inserted表中检查插入的数据是否满足业务需求
>
> * 如果不满足，则向用户报告错误消息，并回滚插入操作
>
> Deleted 表
>
> * 临时保存了删除或更新前的记录行 
>
> * 可以从deleted表中检查被删除的数据是否满足业务需求
>
> * 如果不满足，则向用户报告错误消息，并回滚插入操作

**raiserror（）函数：**

~~~
/*
1. 第一个参数：错误的提示信息
2. 第二个参数：错误的级别，0~18
3. 第三个参数：错误的状态码，0~127
*/
raiserror（）
~~~

**update触发器**

~~~sql
/*
实现禁止对某个表的某个字段进行修改
*/
create trigger mytrigger on student
for update
as
begin
	if update(sno)
	begin
		raiserror('不能修改学号', 16, 10)
		rollback transaction
	end
end

update student
set sno = '201866666'
where sno = '201815121'
~~~

> 注意：
>
> * 创建update触发器，禁止对表中的sno数据进行修改，如果用户更新student表的sno项，报错然后进行回滚。

## 8.6 练习（PPT上最后一个题的改版）

~~~sql
--创建一个函数，根据用户指定的学号查看学生的详细信息
create function func(@xuehao char(9))
returns table
as
return(
	select *
	from student
	where sno = @xuehao
)

select *
from dbo.func('201815121')

--创建一个触发器，当执行插入或者更新的时候检查，插入更新的字段是否符合外键约束
create trigger mytrigger on sc
for insert, update
as
begin
	declare @xuehao char(9)
	select @xuehao = inserted.sno from inserted
	
	if not exists(
		select *
		from student
		where sno = @xuehao
	)
	begin
		print('操作有误！！！')
		rollback transaction
	end
end

insert into sc values('201815121', 22, 100)

update sc
set sno = '201811012'
where sno = '201815121'

--创建一个存储过程，根据用户提供的学号查询学生的详细信息
create proc myproc
@tsno char(9),
@ssno char(9) out,
@ssname char(8) out,
@ssage smallint out
as
begin
	select @ssno = sno, @ssname = sname, @ssage = sage
	from student
	where sno = @tsno
end

declare @ssno char(9), @ssname char(8), @ssage smallint
exec myproc '201815121', @ssno out, @ssname out, @ssage out
select @ssno '学号', @ssname '姓名', @ssage '年龄'

--创建一个函数，根据用户提供的学号，返回学生的姓名
create function func(@xuehao char(9))
returns char(8)
as
begin
	return(
		select sname
		from student
		where sno = @xuehao
	)
end

print dbo.func('201815121')
~~~

